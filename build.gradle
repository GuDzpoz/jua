group 'party.iroiro.luajava'
version (System.getenv('IS_RELEASE') == 'true' ? '3.1.3' : '3.1.3-SNAPSHOT')

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects { it ->
    if (!['android', 'android-test'].contains(it.name)) {
        apply plugin: 'java'
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }

            withSourcesJar()
            withJavadocJar()
        }
    }
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdkJavadoc = "https://docs.oracle.com/javase/8/docs/api/"
    println "JDK Javadoc link for this build is ${rootProject.jdkJavadoc}"
}

apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

task allJavadoc(type: Javadoc) {
    source subprojects.findAll { !['example', 'android', 'android-test'].contains(it.name) }
            .collect {
        it.sourceSets.main.allJava
    }
    classpath = files(subprojects.findAll { !['example', 'android', 'android-test'].contains(it.name) }
            .collect {
        it.sourceSets.main.compileClasspath
    })
    exclude '**/party/iroiro/luajava/util/**'
    destinationDir = file("${buildDir}/docs/javadoc")
}
