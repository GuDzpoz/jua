buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.badlogicgames.gdx:gdx-jnigen-gradle:2.3.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
}

repositories {
    mavenCentral()
}

group = rootProject.group
version = rootProject.version

configurations {
    desktopNatives {
        canBeConsumed = true
        canBeResolved = false
    }

    instrumentedJars {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api, implementation, runtimeOnly
    }
}

dependencies {
    api project(':luajava')
}

apply plugin: 'com.badlogicgames.gdx.gdx-jnigen'

jnigen {
    sharedLibName = 'lua51'

    all {
        headerDirs = ['../../jni/luajava', 'lua51/src', 'lua51/etc', 'mod']
        cppExcludes = ['lua51/**/*']
        cExcludes = ['lua51/**/*']
        libraries = ' -lm '
    }

    add(Windows, x32)
    add(Windows, x64)
    String linuxFlags = ' -D_FORTIFY_SOURCE=0 -DLUA_USE_DLOPEN '
    add(Linux, x32) {
        cFlags += linuxFlags
        cppFlags += linuxFlags
    }
    add(Linux, x64) {
        cFlags += linuxFlags
        cppFlags += linuxFlags
    }
    add(Linux, x32, ARM) {
        cFlags += linuxFlags
        cppFlags += linuxFlags
    }
    add(Linux, x64, ARM) {
        cFlags += linuxFlags
        cppFlags += linuxFlags
    }
    String macFlags = ' -DLUA_USE_DLOPEN '
    add(MacOsX, x64) {
        libraries = ''
        cFlags += macFlags
        cppFlags += macFlags
    }
    add(MacOsX, x64, ARM) {
        libraries = ''
        cFlags += macFlags
        cppFlags += macFlags
    }
    add(Android) {
        String androidFlags = ' -D_FORTIFY_SOURCE=1 -DLUA_USE_DLOPEN '
        cFlags += androidFlags
        cppFlags += androidFlags
        androidApplicationMk = [
                'APP_PLATFORM := android-19',
                'APP_STL := c++_static',
                "APP_CFLAG :=$androidFlags",
        ]
    }
    add(IOS) {
        libraries = ''
    }
}

artifacts {
    instrumentedJars(jar)
    desktopNatives(jnigenJarNativesDesktop)
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'party.iroiro.luajava.lua51')
    }
}

tasks.jnigen.dependsOn(tasks.build)
